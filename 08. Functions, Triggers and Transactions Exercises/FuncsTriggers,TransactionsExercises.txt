TASK 1

CREATE PROC usp_GetEmployeesSalaryAbove35000 
AS
SELECT FirstName, LastName FROM Employees
WHERE Salary > 35000

TASK 2

CREATE PROC usp_GetEmployeesSalaryAboveNumber (@number money)
AS
SELECT FirstName, LastName FROM Employees
WHERE Salary >= @number

TASK 3

CREATE PROC usp_GetTownsStartingWith (@string varchar(max))
AS
SELECT Name FROM Towns
WHERE Name LIKE @string + '%'

TASK 4

CREATE PROC usp_GetEmployeesFromTown (@town varchar(max))
AS
SELECT FirstName, LastName FROM Employees AS e
JOIN Addresses AS a
ON e.AddressId = a.AddressId 
JOIN Towns AS t
ON a.TownId = t.TownId
WHERE t.Name = @town

TASK 5

CREATE FUNCTION ufn_GetSalaryLevel(@salary MONEY) 
RETURNS VARCHAR(10)
AS 
BEGIN 
	DECLARE @result VARCHAR(10)
	IF(@salary < 30000)
	BEGIN
		SET @result = 'Low'
	END
	ELSE IF(@salary >= 30000 AND @salary <=50000)
	BEGIN 
		SET @result = 'Average'
	END
	ELSE IF(@salary > 50000)
	BEGIN 
		SET @result = 'High'
	END
	return @result
END

TASK 6

CREATE PROC usp_EmployeesBySalaryLevel (@salaryLevel VARCHAR(10))
AS
BEGIN
SELECT FirstName, LastName FROM Employees
WHERE dbo.ufn_GetSalaryLevel(Salary) = @salaryLevel
END

TASK 7

CREATE FUNCTION ufn_IsWordComprised(@setOfLetters VARCHAR(MAX), @word VARCHAR(MAX)) 
RETURNS BIT
AS
BEGIN 
	DECLARE @length INT = LEN(@word)
	DECLARE @index INT = 1

	WHILE(@index <= @length)
	BEGIN
		DECLARE @char VARCHAR = SUBSTRING(@word, @index, 1)
		IF(CHARINDEX(@char, @setOfLetters) <= 0)
		BEGIN
			RETURN 0
		END
		ELSE 
		SET @index += 1
	END

	RETURN 1
END

TASK 8 FIRST SOLUTION 

alter table EmployeesProjects
drop constraint FK_EmployeesProjects_Employees

alter table EmployeesProjects
ADD CONSTRAINT FK_EmployeesProjects_Employees Foreign key (EmployeeID) references Employees(EmployeeID) ON DELETE cascade

alter table Departments 
drop constraint FK_Departments_Employees

alter table Departments 
alter column ManagerID int

alter table Departments
add constraint FK_Departments_Employees Foreign key (ManagerID) references Employees(EmployeeID) ON DELETE set null

alter table Employees 
drop constraint [FK_Employees_Employees]

alter table Employees 
alter column ManagerID int

delete from Employees
where DepartmentID in (
 select d.DepartmentID
FROM Departments as d
WHERE d.Name IN 
(
'Production','Production Control'
)
)

DELete  from Departments
where ManagerID is null

TASK 8 SECOND SOLUTION HAVE TO DELETE FK FIRST

DELETE FROM EmployeeProjects 
	WHERE EmployeeID IN (
	SELECT e.EmployeeId FROM Employees AS e
	JOIN Departments AS d
	ON e.DepartmentID = d.DepartmentID
	WHERE d.Name IN ('Production', 'Production Control')
)

UPDATE Employees
SET ManagerID = NULL
WHERE ManagerID IN (
	SELECT e.EmployeeId FROM Employees AS e
	JOIN Departments AS d
	ON e.DepartmentID = d.DepartmentID
	WHERE d.Name IN ('Production', 'Production Control')
) 

UPDATE Departments
SET ManagerID = NULL
WHERE ManagerID IN (
	SELECT e.EmployeeId FROM Employees AS e
	JOIN Departments AS d
	ON e.DepartmentID = d.DepartmentID
	WHERE d.Name IN ('Production', 'Production Control')
)

DELETE FROM Employees
WHERE EmployeeID IN (
	SELECT e.EmployeeId FROM Employees AS e
	JOIN Departments AS d
	ON e.DepartmentID = d.DepartmentID
	WHERE d.Name IN ('Production', 'Production Control')
)

DELETE FROM Departments
WHERE Name IN ('Production', 'Production Control')

TASK 9

CREATE PROCEDURE usp_AssignProject (@employeeID INT, @projectID INT)
AS
BEGIN
	DECLARE @maxEmployeeProjectsCount INT = 3;
	DECLARE @employeeProjectsCount INT;

	BEGIN TRAN
	INSERT INTO EmployeesProjects (EmployeeID, ProjectID) 
	VALUES (@employeeID, @projectID)

	SET @employeeProjectsCount =
	(
		SELECT COUNT(*)
		FROM EmployeesProjects
		WHERE EmployeeID = @employeeID
	)

	IF(@employeeProjectsCount > @maxEmployeeProjectsCount)
		BEGIN
			RAISERROR('The employee has too many projects!', 16, 1);
			ROLLBACK;
		END
	ELSE COMMIT
END

TASK 10

CREATE PROC usp_GetHoldersFullName 
AS
BEGIN 
	SELECT FirstName + ' ' + LastName FROM AccountHolders
END

TASK 11

CREATE PROCEDURE usp_GetHoldersWithBalanceHigherThan(@number MONEY)
AS
BEGIN
SELECT FirstName AS 'First Name', LastName AS 'Last Name' 
FROM (SELECT FirstName, LastName,
	SUM(a.Balance) AS TotalBalance 
	FROM AccountHolders AS ah
	JOIN Accounts AS a
	ON a.AccountHolderId = ah.Id
	GROUP BY ah.FirstName, ah.LastName
	) AS tb
	WHERE tb.TotalBalance > @number
END

TASK 12

CREATE FUNCTION ufn_CalculateFutureValue 
	(@sum MONEY, @annualIntRate FLOAT, @years INT)
RETURNS MONEY
AS
BEGIN
	RETURN @sum * POWER(1 + @annualIntRate, @years);  
END;

TASK 13

CREATE PROCEDURE usp_CalculateFutureValueForAccount 
	(@accountId INT, @interestRate FLOAT)
AS
BEGIN
	DECLARE @years INT = 5
	SELECT
		a.Id AS [Account Id], 
		ah.FirstName, 
		ah.LastName, 
		a.Balance AS [CurrentBalance],
		dbo.ufn_CalculateFutureValue (a.Balance, @interestRate, @years) AS [Balance in 5 years]
	FROM AccountHolders AS ah
	JOIN Accounts AS a
		ON ah.Id = a.AccountHolderId
	WHERE a.Id = @accountId
END

TASK 14

CREATE PROCEDURE usp_DepositMoney (@accountId INT, @moneyAmount MONEY)
AS 
BEGIN
	BEGIN TRAN
	UPDATE Accounts
	SET Balance += @moneyAmount
	WHERE Id = @accountId

	IF (@@ROWCOUNT > 1)
	BEGIN
		ROLLBACK;
		RAISERROR ('Invalid account!', 16, 1);
		RETURN;
	END
	COMMIT
END

TASK 15

CREATE PROCEDURE usp_WithdrawMoney (@accountId INT, @moneyAmount MONEY)
AS 
BEGIN
	BEGIN TRAN
	UPDATE Accounts
	SET Balance -= @moneyAmount
	WHERE Id = @accountId

	IF (@@ROWCOUNT > 1)
	BEGIN
		ROLLBACK;
		RAISERROR ('Invalid account!', 16, 1);
		RETURN;
	END
	COMMIT
END

TASK 16

CREATE PROCEDURE usp_TransferMoney 
	(@senderId INT, @receiverId INT, @transferAmount MONEY)
AS
BEGIN 
	DECLARE @senderBalanceBefore MONEY = 
	(SELECT Balance FROM Accounts WHERE Id = @senderId);
	
	IF(@senderBalanceBefore IS NULL)
		BEGIN
		RAISERROR('Invalid sender account!', 16, 1);
		RETURN;
	END   
	DECLARE @receiverBalanceBefore MONEY = 
	(SELECT Balance FROM Accounts WHERE Id = @receiverId);  

	IF(@receiverBalanceBefore IS NULL)
	BEGIN
		RAISERROR('Invalid receiver account!', 16, 1);
		RETURN;
	END  
	 
	IF(@senderId = @receiverId)
	BEGIN
		RAISERROR('Sender and receiver accounts must be different!', 16, 1);
		RETURN;
	END 
	 
	IF(@transferAmount <= 0)
	BEGIN
		RAISERROR ('Transfer amount must be positive!', 16, 1); 
		RETURN;
	END 
  
	BEGIN TRANSACTION
	EXEC usp_WithdrawMoney @senderId, @transferAmount;
	EXEC usp_DepositMoney @receiverId, @transferAmount;
  
	 DECLARE @senderBalanceAfter MONEY = 
		(SELECT Balance FROM Accounts WHERE Id = @senderId);

	DECLARE @receiverBalanceAfter MONEY = 
		(SELECT Balance FROM Accounts WHERE Id = @receiverId);  
	
	IF((@senderBalanceAfter <> @senderBalanceBefore - @transferAmount) OR 
     (@receiverBalanceAfter <> @receiverBalanceBefore + @transferAmount))
	BEGIN
		ROLLBACK;
		RAISERROR('Invalid account balances!', 16, 1);
		RETURN;
	END
	COMMIT;
END

TASK 17

CREATE TRIGGER T_Accounts_After_Update ON Accounts AFTER UPDATE
AS 
BEGIN
	INSERT INTO Logs(AccountId, OldSum, NewSum)
	SELECT i.Id, d.Balance, i.Balance FROM inserted AS i
	JOIN deleted AS d
	ON d.Id = i.Id
END

TASK 18

CREATE TRIGGER tr_LogsNotificationEmails ON Logs FOR INSERT
AS 
BEGIN
	DECLARE @recipient INT = (SELECT AccountId FROM inserted)
	DECLARE @oldBalance MONEY = (SELECT OldSum FROM inserted)
	DECLARE @newBalance MONEY = (SELECT NewSum FROM inserted)
	DECLARE @subject VARCHAR(200) = CONCAT('Balance change for account: ', @recipient);
	DECLARE @body VARCHAR(200) = CONCAT('On ', GETDATE(), ' your balance was changed from ', @oldBalance, ' to ', @newBalance, '.');  
	INSERT INTO NotificationEmails (Recipient, Subject, Body)
	VALUES (@recipient, @subject, @body)
END

--- SOLUTION 2

CREATE TRIGGER tr_LogsNotificationEmails ON Logs FOR INSERT
AS
BEGIN
	INSERT INTO NotificationEmails (Recipient, Subject, Body)
	(SELECT AccountId,
		CONCAT('Balance change for account: ', AccountId ),
		CONCAT('On ', GETDATE(), 'your balance was changed from ', OldSum, 
		' to ', NewSum, '.')
	FROM inserted)
END

TASK 19

CREATE FUNCTION ufn_CashInUsersGames(@gameName VARCHAR(50))
RETURNS Table
AS 
RETURN 
(
	WITH CTE_CashInRows (Cash, RowNumber) AS 
	(
		SELECT ug.Cash, ROW_NUMBER() OVER (ORDER BY ug.Cash DESC)
		FROM UsersGames AS ug
		JOIN Games AS g 
		ON ug.GameId = g.Id
		WHERE g.Name = @gameName
	)

	SELECT SUM(cash) AS SumCash
	FROM CTE_CashInRows
	WHERE RowNumber % 2 = 1
)

TASK 20

CREATE TRIGGER tr_ItemsPurchaseRestrictions ON UserGameItems FOR INSERT
AS
BEGIN
  DECLARE @userGameLevel int = (
    SELECT ug.Level
    FROM inserted AS ugi -- UserGameItems
    JOIN UsersGames AS ug ON ugi.UserGameId = ug.Id
  );
  DECLARE @itemMinLevel int = (
    SELECT i.MinLevel
    FROM inserted AS ugi -- UserGameItems
    JOIN Items AS i on i.Id = ugi.ItemId
  );
  IF(@itemMinLevel > @userGameLevel)
    BEGIN
      ROLLBACK;
      RAISERROR('Higher user game level required for item purchase', 16, 1);
      RETURN;
    END
END

TASK 21

DECLARE @gameName nvarchar(50) = 'Safflower';
DECLARE @username nvarchar(50) = 'Stamat';
DECLARE @userGameId int = (
  SELECT ug.Id 
  FROM UsersGames AS ug
  JOIN Users AS u ON ug.UserId = u.Id
  JOIN Games AS g ON ug.GameId = g.Id
  WHERE u.Username = @username AND g.Name = @gameName
);
DECLARE @userGameLevel int = (SELECT Level FROM UsersGames WHERE Id = @userGameId);
DECLARE @itemsCost money, @availableCash money, @minLevel int, @maxLevel int;

SET @minLevel = 11; SET @maxLevel = 12;
SET @availableCash = (SELECT Cash FROM UsersGames WHERE Id = @userGameId);
SET @itemsCost = (SELECT SUM(Price) FROM Items WHERE MinLevel BETWEEN @minLevel AND @maxLevel);

IF (@availableCash >= @itemsCost AND @userGameLevel >= @maxLevel) 
BEGIN 
	BEGIN TRANSACTION  
	UPDATE UsersGames SET Cash -= @itemsCost WHERE Id = @userGameId; 
	IF(@@ROWCOUNT <> 1) 
	BEGIN
			ROLLBACK; RAISERROR('Could not make payment', 16, 1); --RETURN;
	END
	ELSE
	BEGIN
	  INSERT INTO UserGameItems (ItemId, UserGameId) 
			(SELECT Id, @userGameId FROM Items WHERE MinLevel BETWEEN @minLevel AND @maxLevel) 
	  IF((SELECT COUNT(*) FROM Items WHERE MinLevel BETWEEN @minLevel AND @maxLevel) <> @@ROWCOUNT)
	  BEGIN
			ROLLBACK; RAISERROR('Could not buy items', 16, 1); --RETURN;
	  END	
	  ELSE COMMIT;
	END
END

SET @minLevel = 19; SET @maxLevel = 21;
SET @availableCash = (SELECT Cash FROM UsersGames WHERE Id = @userGameId);
SET @itemsCost = (SELECT SUM(Price) FROM Items WHERE MinLevel BETWEEN @minLevel AND @maxLevel);

IF (@availableCash >= @itemsCost AND @userGameLevel >= @maxLevel) 
BEGIN 
	BEGIN TRANSACTION  
	UPDATE UsersGames SET Cash -= @itemsCost WHERE Id = @userGameId; 
	IF(@@ROWCOUNT <> 1) 
	BEGIN
		ROLLBACK; RAISERROR('Could not make payment', 16, 1); --RETURN;
	END
	ELSE
	BEGIN
		INSERT INTO UserGameItems (ItemId, UserGameId) 
		(SELECT Id, @userGameId FROM Items WHERE MinLevel BETWEEN @minLevel AND @maxLevel) 
		IF((SELECT COUNT(*) FROM Items WHERE MinLevel BETWEEN @minLevel AND @maxLevel) <> @@ROWCOUNT)
		BEGIN
		  ROLLBACK; RAISERROR('Could not buy items', 16, 1); --RETURN;
		END	
		ELSE COMMIT;
	END
END

SELECT i.Name AS [Item Name]
FROM UserGameItems AS ugi
JOIN Items AS i		
	ON i.Id = ugi.ItemId
JOIN UsersGames AS ug 
	ON ug.Id = ugi.UserGameId
JOIN Games AS g 
	ON g.Id = ug.GameId
WHERE g.Name = @gameName
ORDER BY [Item Name]

TASK 22

SELECT 
	RIGHT(Email, LEN(Email) - CHARINDEX('@', Email)) AS [Email Provider],
	COUNT(*) AS [Number of Users]
FROM Users
GROUP BY RIGHT(Email, LEN(Email) - CHARINDEX('@', Email))
ORDER BY [Number of Users] DESC, [Email Provider]

TASK 23

SELECT g.Name AS Game, gt.Name AS [Game Type],
  u.Username, ug.Level, ug.Cash, c.Name AS Character
FROM UsersGames AS ug
JOIN Games AS g 
	ON ug.GameId = g.Id
JOIN GameTypes AS gt 
	ON g.GameTypeId = gt.Id
JOIN Users AS u 
	ON ug.UserId = u.Id
JOIN Characters AS c 
	ON ug.CharacterId = c.Id
ORDER BY ug.Level DESC, u.Username, Game

TASK 24

SELECT u.Username, g.Name AS Game,
	COUNT(ugi.ItemId) AS [Items Count], SUM(i.Price) AS [Items Price]
FROM UsersGames AS ug
JOIN Users AS u 
	ON ug.UserId = u.Id
JOIN Games AS g 
	ON ug.GameId = g.Id
JOIN UserGameItems AS ugi 
	ON ugi.UserGameId = ug.Id
JOIN Items AS i 
	ON ugi.ItemId = i.Id
GROUP BY u.Username, g.Name
HAVING COUNT(ugi.ItemId) >= 10
ORDER BY [Items Count] DESC, [Items Price] DESC, u.Username

TASK 25

SELECT 
	u.Username, g.Name AS Game, MAX(c.Name) AS Character, 
	MAX(cs.Strength) + MAX(gts.Strength) + SUM(gis.Strength) AS Strength, 
	MAX(cs.Defence) + MAX(gts.Defence) + SUM(gis.Defence) AS Defence, 
	MAX(cs.Speed) + MAX(gts.Speed) + SUM(gis.Speed) AS Speed, 
	MAX(cs.Mind) + MAX(gts.Mind) + SUM(gis.Mind) AS Mind, 
	MAX(cs.Luck) + MAX(gts.Luck) + SUM(gis.Luck) AS Luck
FROM UsersGames AS ug
JOIN Users AS u 
	ON ug.UserId = u.Id
JOIN Games AS g 
	ON ug.GameId = g.Id
JOIN Characters AS c 
	ON ug.CharacterId = c.Id
JOIN [Statistics] AS cs 
	ON c.StatisticId = cs.Id
JOIN GameTypes AS gt 
	ON gt.Id = g.GameTypeId
JOIN [Statistics] AS gts 
	ON gts.Id = gt.BonusStatsId
JOIN UserGameItems AS ugi 
	ON ugi.UserGameId = ug.Id
JOIN Items AS i 
	ON i.Id = ugi.ItemId
JOIN [Statistics] AS gis 
	ON gis.Id = i.StatisticId
GROUP BY u.Username, g.Name
ORDER BY Strength DESC, Defence DESC, Speed DESC, Mind DESC, Luck DESC

TASK 26

WITH CTE_AboveAverageStats (Id) AS 
(  
	SELECT Id FROM [Statistics]
	WHERE Mind > (SELECT AVG(Mind  * 1.0) FROM [Statistics]) AND
		Luck > (SELECT AVG(Luck  * 1.0) FROM [Statistics]) AND
		Speed > (SELECT AVG(Speed * 1.0) FROM [Statistics])
)
SELECT 
	i.Name, i.Price, i.MinLevel, s.Strength, s.Defence, s.Speed, 
	s.Luck, s.Mind
FROM CTE_AboveAverageStats AS av
JOIN [Statistics] AS s 
	ON av.Id = s.Id
JOIN Items AS i 
	ON i.StatisticId = s.Id
ORDER BY i.Name

TASK 27

SELECT i.Name AS Item, i.Price, i.MinLevel, gt.Name AS [Forbidden Game Type]
FROM Items AS i
LEFT JOIN GameTypeForbiddenItems AS fi 
	ON fi.ItemId = i.Id
LEFT JOIN GameTypes AS gt 
	ON fi.GameTypeId = gt.Id
ORDER BY [Forbidden Game Type] DESC, Item

TASK 28

DECLARE @gameName nvarchar(50) = 'Edinburgh';
DECLARE @username nvarchar(50) = 'Alex';
DECLARE @userGameId int = (
	SELECT ug.Id 
	FROM UsersGames AS ug
	JOIN Users AS u 
		ON ug.UserId = u.Id
	JOIN Games AS g 
		ON ug.GameId = g.Id
	WHERE u.Username = @username AND g.Name = @gameName
);
DECLARE @availableCash money = (SELECT Cash FROM UsersGames WHERE Id = @userGameId);
DECLARE @purchasePrice money = (
  SELECT SUM(Price) FROM Items WHERE Name IN 
	('Blackguard', 'Bottomless Potion of Amplification', 'Eye of Etlich (Diablo III)',
	'Gem of Efficacious Toxin', 'Golden Gorget of Leoric', 'Hellfire Amulet')
); 

IF (@availableCash >= @purchasePrice) 
BEGIN 
	BEGIN TRANSACTION  
	UPDATE UsersGames SET Cash -= @purchasePrice WHERE Id = @userGameId; 
	IF(@@ROWCOUNT <> 1) 
	BEGIN
		ROLLBACK; RAISERROR('Could not make payment', 16, 1); RETURN;
	END
	INSERT INTO UserGameItems (ItemId, UserGameId) 
	(SELECT Id, @userGameId FROM Items WHERE Name IN 
		('Blackguard', 'Bottomless Potion of Amplification', 'Eye of Etlich (Diablo III)',
		'Gem of Efficacious Toxin', 'Golden Gorget of Leoric', 'Hellfire Amulet')) 
	IF((SELECT COUNT(*) FROM Items WHERE Name IN 
		('Blackguard', 'Bottomless Potion of Amplification', 'Eye of Etlich (Diablo III)', 
			'Gem of Efficacious Toxin', 'Golden Gorget of Leoric', 'Hellfire Amulet')) <> @@ROWCOUNT)
	BEGIN
		ROLLBACK; RAISERROR('Could not buy items', 16, 1); RETURN;
	END	
	COMMIT;
END

SELECT u.Username, g.Name, ug.Cash, i.Name AS [Item Name]
FROM UsersGames AS ug
JOIN Games AS g 
	ON ug.GameId = g.Id
JOIN Users AS u 
	ON ug.UserId = u.Id
JOIN UserGameItems AS ugi 
	ON ug.Id = ugi.UserGameId
JOIN Items AS i 
	ON i.Id = ugi.ItemId
WHERE g.Name = @gameName

TASK 29

SELECT p.PeakName, m.MountainRange AS Mountain, p.Elevation
FROM Peaks AS p
JOIN Mountains AS m
	ON p.MountainId = m.Id
ORDER BY p.Elevation DESC, p.PeakName

TASK 30

SELECT p.PeakName, m.MountainRange 
	AS Mountain, c.CountryName, cont.ContinentName
FROM Peaks AS p
JOIN Mountains AS m 
	ON p.MountainId = m.Id
JOIN MountainsCountries AS mc 
	ON m.Id = mc.MountainId
JOIN Countries AS c 
	ON mc.CountryCode = c.CountryCode
JOIN Continents AS cont 
	ON c.ContinentCode = cont.ContinentCode
ORDER BY p.PeakName, c.CountryName

TASK 31

SELECT c.CountryName, cont.ContinentName, 
  COUNT(r.Id) AS RiverCount, ISNULL(SUM(r.Length), 0) AS TotalLength
FROM Countries AS c
LEFT JOIN CountriesRivers AS cr
	ON c.CountryCode = cr.CountryCode
LEFT JOIN Rivers AS r 
	ON cr.RiverId = r.Id
LEFT JOIN Continents AS cont 
	ON c.ContinentCode = cont.ContinentCode
GROUP BY c.CountryName, cont.ContinentName
ORDER BY RiverCount DESC, TotalLength DESC, c.CountryName

TASK 32

SELECT cy.CurrencyCode, cy.Description AS Currency, COUNT(c.CountryCode) AS NumberOfCountries
FROM Currencies AS cy
LEFT JOIN Countries AS c 
	ON c.CurrencyCode = cy.CurrencyCode
GROUP BY cy.CurrencyCode, cy.Description
ORDER BY NumberOfCountries DESC, Currency

TASK 33

SELECT ct.ContinentName, SUM(c.AreaInSqKm) AS CountriesArea, SUM(CAST(c.Population AS float)) AS CountriesPopulation
FROM Continents AS ct
LEFT JOIN Countries AS c 
	ON ct.ContinentCode = c.ContinentCode
GROUP BY ct.ContinentName
ORDER BY CountriesPopulation DESC

TASK 34

CREATE TABLE Monasteries(
	Id int NOT NULL IDENTITY, 
	Name nvarchar(200) NOT NULL, 
	CountryCode char(2) NOT NULL,
	CONSTRAINT PK_Monasteries PRIMARY KEY (Id),
	CONSTRAINT FK_Monasteries_Countries FOREIGN KEY (CountryCode) REFERENCES Countries(CountryCode)
)

INSERT INTO Monasteries(Name, CountryCode) VALUES
	('Rila Monastery “St. Ivan of Rila”', 'BG'), 
	('Bachkovo Monastery “Virgin Mary”', 'BG'),
	('Troyan Monastery “Holy Mother''s Assumption”', 'BG'),
	('Kopan Monastery', 'NP'),
	('Thrangu Tashi Yangtse Monastery', 'NP'),
	('Shechen Tennyi Dargyeling Monastery', 'NP'),
	('Benchen Monastery', 'NP'),
	('Southern Shaolin Monastery', 'CN'),
	('Dabei Monastery', 'CN'),
	('Wa Sau Toi', 'CN'),
	('Lhunshigyia Monastery', 'CN'),
	('Rakya Monastery', 'CN'),
	('Monasteries of Meteora', 'GR'),
	('The Holy Monastery of Stavronikita', 'GR'),
	('Taung Kalat Monastery', 'MM'),
	('Pa-Auk Forest Monastery', 'MM'),
	('Taktsang Palphug Monastery', 'BT'),
	('Sumela Monastery', 'TR');


WITH CTE_CountriesWithMoreRivers (CountryCode) AS (
	SELECT c.CountryCode
	FROM Countries AS c
	JOIN CountriesRivers AS cr 
		ON c.CountryCode = cr.CountryCode
	GROUP BY c.CountryCode
	HAVING COUNT(cr.RiverId) > 3
)
UPDATE Countries
SET IsDeleted = 1
WHERE CountryCode IN (SELECT * FROM CTE_CountriesWithMoreRivers)

SELECT m.Name AS Monastery, c.CountryName AS Country
FROM Monasteries AS m
JOIN Countries AS c 
	ON c.CountryCode = m.CountryCode
WHERE c.IsDeleted = 0
ORDER BY Monastery

TASK 35

UPDATE Countries
SET CountryName = 'Burma'
WHERE CountryName = 'Myanmar'

INSERT INTO Monasteries (Name, CountryCode)
(SELECT 'Hanga Abbey', CountryCode FROM Countries AS c 
 WHERE CountryName = 'Tanzania')

 INSERT INTO Monasteries (Name, CountryCode)
(SELECT 'Myin-Tin-Daik', CountryCode FROM Countries AS c 
 WHERE CountryName = 'Myanmar')

SELECT cont.ContinentName, c.CountryName, 
	COUNT(m.Name) AS MonasteriesCount
FROM Continents AS cont
	LEFT JOIN Countries AS c 
		ON cont.ContinentCode = c.ContinentCode
	LEFT JOIN Monasteries AS m 
		ON m.CountryCode = c.CountryCode
WHERE c.IsDeleted = 0
GROUP BY cont.ContinentName, c.CountryName
ORDER BY MonasteriesCount DESC, c.CountryName